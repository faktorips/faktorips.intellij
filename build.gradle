import org.jetbrains.intellij.platform.gradle.IntelliJPlatformType
import org.jetbrains.intellij.platform.gradle.models.ProductRelease

plugins {
    id 'org.jetbrains.intellij.platform' version '2.2.0'
    id 'java'
}

group 'org.faktorips'

repositories {
    mavenCentral()

    intellijPlatform {
        defaultRepositories()
    }
}

dependencies {
    intellijPlatform {
        intellijIdeaCommunity("2022.3")

        bundledPlugin("com.intellij.java")

        pluginVerifier()
        zipSigner()
        instrumentationTools()
    }
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.0'
}

intellijPlatform {
    version = '25.1'
    buildSearchableOptions = false
    pluginConfiguration {
        ideaVersion {
            changeNotes = "Alpha Version"
            sinceBuild = "223"
            untilBuild = project.providers.<String>provider { null }
        }
    }
    pluginVerification {
        ides {
            select {
                it.types = [IntelliJPlatformType.IntellijIdeaCommunity]
                it.channels = [ProductRelease.Channel.RELEASE]
                it.sinceBuild = '223'
                it.untilBuild = project.providers.<String>provider { null }
            }
        }
    }
    signing {
        certificateChain = System.getenv('INTELLIJ_CERTIFICATE_CHAIN')!=null?new File(System.getenv('INTELLIJ_CERTIFICATE_CHAIN')).getText('UTF-8'):''
        privateKey = System.getenv('INTELLIJ_PRIVATE_KEY')!=null?new File(System.getenv('INTELLIJ_PRIVATE_KEY')).getText('UTF-8'):''
        password = System.getenv('INTELLIJ_PRIVATE_KEY_PASSWORD')?:''
    }
    publishing {
        token = System.getenv('INTELLIJ_PUBLISH_TOKEN')?:''
        channels = [System.getenv('INTELLIJ_CHANNEL')?:'Alpha']
    }
}
test {
    useJUnitPlatform()
}
