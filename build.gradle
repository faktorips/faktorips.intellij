import org.jetbrains.intellij.platform.gradle.IntelliJPlatformType
import org.jetbrains.intellij.platform.gradle.models.ProductRelease

plugins {
    id 'org.jetbrains.intellij.platform' version '2.0.1'
    id 'java'
}

group 'org.faktorips'

repositories {
    mavenCentral()

    intellijPlatform {
        defaultRepositories()
    }
}

dependencies {
    intellijPlatform {
        intellijIdeaCommunity("2022.3")

        bundledPlugin("com.intellij.java")

        pluginVerifier()
        zipSigner()
        instrumentationTools()
    }
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.0'
}

intellijPlatform {
    version = '24.7'
    buildSearchableOptions = false
    pluginVerification {
        ides {
            select {
                it.types = [IntelliJPlatformType.IntellijIdeaCommunity]
                it.channels = [ProductRelease.Channel.RELEASE]
                it.sinceBuild = '223'
            }
        }
    }
}
patchPluginXml {
    changeNotes = """
      Alpha Version
      """
    sinceBuild = "223"
    untilBuild = "243.*"
}
test {
    useJUnitPlatform()
}

signPlugin {
    certificateChain = System.getenv('INTELLIJ_CERTIFICATE_CHAIN')!=null?new File(System.getenv('INTELLIJ_CERTIFICATE_CHAIN')).getText('UTF-8'):''
    privateKey = System.getenv('INTELLIJ_PRIVATE_KEY')!=null?new File(System.getenv('INTELLIJ_PRIVATE_KEY')).getText('UTF-8'):''
    password = System.getenv('INTELLIJ_PRIVATE_KEY_PASSWORD')?:''
}

publishPlugin {
    token = System.getenv('INTELLIJ_PUBLISH_TOKEN')?:''
    channels = [System.getenv('INTELLIJ_CHANNEL')?:'Alpha']
}