plugins {
    id 'org.jetbrains.intellij.platform' version '2.3.0'
    id 'java'
}

group 'org.faktorips'
// version is in gradle.properties file, for easy automated access

repositories {
    mavenCentral()

    intellijPlatform {
        defaultRepositories()
    }
}

dependencies {
    intellijPlatform {
        intellijIdeaCommunity '2022.3'
        bundledPlugin 'com.intellij.java'

        pluginVerifier()
        zipSigner()
        instrumentationTools()
    }
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.0'
}

intellijPlatform {
    buildSearchableOptions = false
    instrumentCode = true
    pluginConfiguration {
        ideaVersion {
            sinceBuild = '223'
            untilBuild = provider { null } as Provider<? extends String>
        }
    }
    pluginVerification {
        ides {
            recommended()
        }
    }
    patchPluginXml {
        changeNotes = """
        Alpha Version
        """.stripIndent()
    }
    signing {
        certificateChain = System.getenv('INTELLIJ_CERTIFICATE_CHAIN')!=null?new File(System.getenv('INTELLIJ_CERTIFICATE_CHAIN')).getText('UTF-8'):''
        privateKey = System.getenv('INTELLIJ_PRIVATE_KEY')!=null?new File(System.getenv('INTELLIJ_PRIVATE_KEY')).getText('UTF-8'):''
        password = System.getenv('INTELLIJ_PRIVATE_KEY_PASSWORD')?:''
    }
    publishing {
        token = System.getenv('INTELLIJ_PUBLISH_TOKEN')?:''
        channels = [System.getenv('INTELLIJ_CHANNEL')?:'Alpha']
    }
}
test {
    useJUnitPlatform()
}
